plugins {
    id 'com.github.johnrengelman.shadow'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id "org.jetbrains.kotlin.kapt"
}

dependencies {
    implementation project(":lua-mirai-core")
    runtimeOnly project(":lua-mirai-adapter-luakt")
    // runtimeOnly project(":lua-mirai-adapter-rhinojs")
    compileOnly 'net.mamoe:mirai-console:2.10.3'
    testImplementation 'net.mamoe:mirai-console-terminal:2.10.3'
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.2"
    implementation 'com.google.auto.service:auto-service:1.1.1'
    kapt 'com.google.auto.service:auto-service:1.1.1'
}

tasks.register('createPluginYml') {
    dependsOn processResources
    doLast {
        new File("$buildDir/resources/main/plugin.yml").withWriter { w ->
            w.writeLine('id: "com.github.only52607.luamirai"')
            w.writeLine("name: \"${rootProject.name}\"")
            w.writeLine('main: ""')
            w.writeLine("version: \"${project.version.toString()}\"")
        }
    }
}

classes {
    dependsOn createPluginYml
}

shadowJar {
    mergeServiceFiles()
    dependencies {
        exclude(dependency('net.mamoe:mirai-.*'))
    }
}